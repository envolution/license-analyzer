pkgname=python-license-analyzer-git
_pkgname=license-analyzer
pkgver=0.0.1
pkgrel=1
pkgdesc="compare against SPDX licenses using sha256/fingerprinting/semantic embeddings"
arch=('any')
url='https://github.com/envolution/license-analyzer'
license=(Apache-2.0)
depends=(python python-numpy python-sentence-transformers pytorch python-transformers)
makedepends=(
  python-build
  python-installer
  git
)
#source=("$url/archive/refs/tags/${pkgver}.tar.gz")
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/+/g'
}

build() {
  cd $_pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname
  PYTHONPATH=src python -m pytest tests/
}

package() {
  cd $_pkgname
  python -m installer --destdir="$pkgdir" dist/${_pkgname/-/_}-${pkgver%%+*}*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
